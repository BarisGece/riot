plugins {
    id 'net.researchgate.release' version '2.8.1'
    id 'org.asciidoctor.jvm.convert' version '3.2.0'
    id 'org.ajoberstar.git-publish' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.34.0'
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

subprojects {
    group = 'com.redislabs.riot'
    apply plugin: 'java'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    tasks.withType(Tar) {
        compression = Compression.GZIP
    }
}

release {
    tagTemplate = 'v${version}'
}

asciidoctor {
    baseDir = "docs"
    sourceDir = "docs"
    resources {
        from("${sourceDir}/images") {
            exclude '*.excalidraw'
            into 'images'
        }
        from("riot-file/src/test/resources/csv") {
        	include '*.csv'
        	into '.'
        }
        from("riot-file/src/test/resources/json") {
        	include '*.json'
        	into '.'
        }
        from("riot-file/src/test/resources/xml") {
        	include '*.xml'
        	into '.'
        }
    }
    
    outputOptions {
        separateOutputDirs = false
    }
}

gitPublish {
    repoUri = 'git@github.com:redis-developer/riot.git'
    referenceRepoUri = 'file:///Users/jruaux/git/riot/'
    branch = 'gh-pages'
    contents {
        from 'build/docs/asciidoc'
    }
}

afterReleaseBuild.dependsOn gitPublishPush
gitPublishPush.dependsOn asciidoctor