plugins {
    id 'application'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
    id 'org.asciidoctor.jvm.convert' version '3.1.0'
    id 'org.ajoberstar.git-publish' version '2.1.3'
}

repositories {
	jcenter()
	mavenCentral()
	mavenLocal()
}

dependencies {
    api 'com.redislabs:picocli-redis:1.13.1'
	implementation 'org.slf4j:slf4j-jdk14:1.7.28'
    implementation 'org.springframework.batch:spring-batch-core:4.2.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.2.5.RELEASE'
    implementation 'org.springframework:spring-jdbc:5.2.4.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-aws-context:2.2.1.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-aws-autoconfigure:2.2.1.RELEASE'
    implementation 'com.github.javafaker:javafaker:1.0.2'
    implementation 'com.zaxxer:HikariCP:3.4.2'
    implementation 'me.tongfei:progressbar:0.8.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.10.3'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    annotationProcessor "info.picocli:picocli-codegen:4.2.0"    
    testImplementation 'org.codehaus.plexus:plexus-utils:3.3.0'
    testImplementation 'org.hsqldb:hsqldb:2.5.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    testImplementation 'it.ozimov:embedded-redis:0.7.2'
//    implementation 'org.xerial:sqlite-jdbc:3.28.0'
//    implementation 'com.ibm.db2:jcc:11.5.0.0'
//    implementation 'com.microsoft.sqlserver:mssql-jdbc:7.4.1.jre8'
//    implementation 'com.oracle.ojdbc:ojdbc8:19.3.0.0'
//    implementation 'org.postgresql:postgresql:42.2.8'
}

test {
    useJUnitPlatform()
}

mainClassName = 'com.redislabs.riot.Riot'

task generateManpageAsciiDoc(type: JavaExec) {
    dependsOn(classes)
    group = "Documentation"
    description = "Generate AsciiDoc manpage"
    classpath(configurations.compile, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    main 'picocli.codegen.docgen.manpage.ManPageGenerator'
    args mainClassName, "--outdir=${project.buildDir}/generated-picocli-docs", "-v" //, "--template-dir=src/docs/mantemplates"
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'riot'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Redislabs-Solution-Architects/riot.git'
	    version {
	        gpg {
	            sign = true
	        }
	        mavenCentralSync {
	            sync = true
	            user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
	            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''
	        }
	    }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.redislabs'
            artifactId 'riot'

            pom {
                name = 'RIOT'
                packaging = 'jar'
                description = 'Redis Input/Output Tool'
                url = 'https://github.com/Redislabs-Solution-Architects/riot'

		        scm {
		          connection = 'scm:git:git://github.com/Redislabs-Solution-Architects/riot.git'
		          developerConnection = 'scm:git:git@github.com:Redislabs-Solution-Architects/riot.git'
		          url = 'https://github.com/Redislabs-Solution-Architects/riot'
		        }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

group = 'com.redislabs'
description = 'RIOT'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

application {
	mainClassName = 'com.redislabs.riot.Riot'
}

jar {
    manifest {
        attributes("Implementation-Title": "RIOT",
                   "Implementation-Version": archiveVersion)
    }
}

tasks.withType(Tar){
    compression = Compression.GZIP
}

asciidoctor {
  attributes revnumber : '1.0'
  outputOptions {
  	separateOutputDirs = false
  }
  attributes \
    'commandsdir': 'src/test/resources/commands'  
}

task printVersion {
    doLast {
        println project.version
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "Redislabs-Solution-Architects"
    repo "riot"
    releaseAssets distZip, distTar
}

gitPublish {
    repoUri = 'git@github.com:Redislabs-Solution-Architects/riot.git'
    referenceRepoUri = 'file:///Users/jruaux/git/riot/'

    branch = 'gh-pages'

    contents {
        from 'build/docs/asciidoc'
    }
}

gitPublishPush.dependsOn asciidoctor

afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn bintrayUpload