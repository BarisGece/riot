plugins {
    id 'org.asciidoctor.jvm.convert'
    id 'org.ajoberstar.git-publish' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'org.jreleaser' version '0.2.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
}

asciidoctor {
    sourceDir 'docs'
    resources {
        from ('docs/images') {
            include 'riot.svg'
            include 'architecture.png'
            include 'mapping.png'
            include 'favicon.svg'
        }
        from("riot-file/src/test/resources/files") {
            include '*.*'
        }
    }
}

gitPublish {
    repoUri = 'https://github.com/redis-developer/riot.git'
    referenceRepoUri = 'file:///Users/jruaux/git/riot/'
    branch = 'gh-pages'
    contents {
        from 'build/docs/asciidoc'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def isNonStable = { String version ->
    def nonStableKeyword = ['PREVIEW'].any { it -> version.toUpperCase().contains(it) }
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE8'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+([.-]r)?$/
    return nonStableKeyword || (!stableKeyword && !(version ==~ regex))
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

jreleaser {
    project {
        name = 'riot'
        description = 'Redis Input/Output Tools'
        longDescription = 'Get data in and out of Redis with RIOT'
        website = 'https://developer.redislabs.com/riot'
        authors = ['Julien Ruaux']
        tags = ['redis']
        license = 'Apache-2.0'
        java {
            multiProject = true
        }
    }
    release {
        github {
            username = 'jruaux'
            owner = 'redis-developer'
            overwrite = true
        }
    }
    //noinspection GroovyAssignabilityCheck
    distributions {
        ['db', 'file', 'gen', 'redis', 'stream'].each { n ->
            "riot-$n" {
                brew {
                    active = 'release'
                    formulaName = "riot-${n}"
                }
                scoop {
                    active = 'release'
                    bucket {
                        name = 'scoop'
                    }
                }
                artifacts {
                    artifact {
                        path = "{{distributionName}}/build/distributions/{{distributionName}}-{{projectVersion}}.zip"
                    }
                }
                java {
                    artifactId = "riot-${n}"
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

gitPublishPush.dependsOn asciidoctor