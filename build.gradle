plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'net.researchgate.release' version '2.8.1'
    id 'org.asciidoctor.jvm.convert' version '3.2.0'
    id 'org.ajoberstar.git-publish' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.33.0'
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

subprojects {
    group = 'com.redislabs.riot'
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    tasks.withType(Tar) {
        compression = Compression.GZIP
    }
}

release {
    tagTemplate = 'v${version}'
}

asciidoctor {
    baseDir = "docs"
    sourceDir = "docs"
    outputOptions {
        separateOutputDirs = false
    }
}

gitPublish {
    repoUri = 'git@github.com:Redislabs-Solution-Architects/riot.git'
    referenceRepoUri = 'file:///Users/jruaux/git/riot/'
    branch = 'gh-pages'
    contents {
        from 'build/docs/asciidoc'
    }
}

afterReleaseBuild.dependsOn gitPublishPush
gitPublishPush.dependsOn asciidoctor