buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:guide-gradle-plugin:$kordampPluginVersion"
        classpath "org.jreleaser:jreleaser-gradle-plugin:$jreleaserPluginVersion"
        classpath "org.ajoberstar:gradle-git-publish:$gitPublishPluginVersion"
        classpath "io.freefair.gradle:lombok-plugin:6.0.0-m2"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.5.1"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'riot'

projects {
    directories = ['core', 'connectors', 'docs']

    plugins {
        all {
            id 'idea'
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
            id 'org.jreleaser'
        }
        path(':guide') {
            id 'org.kordamp.gradle.guide'
            id 'org.ajoberstar.git-publish'
        }
        dirs(['core', 'connectors']) {
            id 'java-library'
            id 'org.springframework.boot'
            id 'io.spring.dependency-management'
            id 'io.freefair.lombok'
        }
        dirs(['connectors']) {
            id 'application'
        }
    }
}

enforce {
    mergeStrategy = 'append'

    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "com.google.guava:guava:30.0-android",
                "com.google.errorprone:error_prone_annotations:2.4.0",
                "com.google.http-client:google-http-client:$googleHttpVersion",
                "com.google.http-client:google-http-client-jackson2:$googleHttpVersion",
                "com.google.api.grpc:proto-google-common-protos:2.0.1",
                "com.google.api:gax:1.60.1",
                "io.grpc:grpc-context:1.34.0",
                "com.google.protobuf:protobuf-java:$protobufVersion",
                "com.google.protobuf:protobuf-java-util:$protobufVersion",
                "org.hdrhistogram:HdrHistogram:2.1.12",
                "com.google.code.findbugs:jsr305:3.0.2"
    }
}
